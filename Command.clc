include "File.clc"

class System
{
    typedef pid_t int;
    typedef wait_status int;
    typedef wait_option int;
    typedef tcflag_t int;
    typedef cc_t byte;

    WNOHANG:static wait_option;
    WUNTRACED:static wait_option;
    WCONTINUED:static wait_option;

    SIGHUP: static int;
    SIGINT: static int;
    SIGQUIT: static int;
    SIGILL: static int;
    SIGABRT: static int;
    SIGFPE: static int;
    SIGKILL: static int;
    SIGSEGV: static int;
    SIGPIPE: static int;
    SIGALRM: static int;
    SIGTERM: static int;
    SIGUSR1: static int;
    SIGUSR2: static int;
    SIGCHLD: static int;
    SIGCONT: static int;
    SIGSTOP: static int;
    SIGTSTP: static int;
    SIGTTIN: static int;
    SIGTTOU: static int;

    TCSANOW: static int;
    TCSADRAIN: static int;
    TCSAFLUSH: static int;

    def initialize_command_system():static native;

    def initialize(): static {
        inherit();
        System.initialize_command_system();
    }
    
    def pipe(read_fd:pointer@int, write_fd:pointer@int): static native throws Exception;
    def fork(block_:lambda()): static native pid_t throws Exception;
    def dup2(fd1:int, fd2:int): static native int throws Exception;
    def execvp(method_name:String, params:List<String>): static native throws Exception;
    def waitpid(pid:pid_t, status:pointer@wait_status, option:wait_option): static native pid_t;

    def WIFEXITED(status:wait_status): static native bool;
    def WEXITSTATUS(status:wait_status): static native int;
    def WIFSIGNALED(status:wait_status): static native bool;
    def WTERMSIG(status:wait_status):static native int;
    def WCOREDUMP(status:wait_status): static native int;
    def WIFSTOPPED(status:wait_status): static native bool;
    def WSTOPSIG(status:wait_status):  static native int;
    def WIFCONTINUED(status:wait_status): static native bool;

    def getpid():static native pid_t;
    def setpgid(pid:pid_t, pgid:pid_t): static native throws Exception;
    def tcsetpgrp(fd:int, pid:pid_t): static native int throws Exception;
    def tcgetattr(fd:int, terminfo:termios): static native throws Exception;
    def tcsetattr(fd:int, optional_actions:int, terminfo:termios): static native throws Exception;
    def kill(pid:pid_t, sig:int): static native throws Exception;
}

class termios
{
    c_iflag:tcflag_t;
    c_oflag:tcflag_t;
    c_cflag:tcflag_t;
    c_lflag:tcflag_t;
    c_cc:cc_t[];

    def initialize() {
        self.c_iflag = 0;
        self.c_oflag = 0;
        self.c_cflag = 0;
        self.c_lflag = 0;
        self.c_cc = new cc_t[32];
    }
}

class Job
{
    title:String;
    pgrp:pid_t;
    terminfo:termios;

    def initialize(title:String, pgrp:pid_t, terminfo:termios) {
        self.title = title;
        self.pgrp = pgrp;
        self.terminfo = terminfo;
    }

    def toString():String {
        return self.title;
    }
    def equals(job:Job): bool {
        return self.pgrp == job.pgrp;
    }
    def forground() {
        terminfo:termios = new termios();
        System.tcgetattr(0, terminfo);

        System.tcsetattr(0, System.TCSANOW, self.terminfo);
        System.tcsetpgrp(0, self.pgrp);

        System.kill(self.pgrp, System.SIGCONT);

        status:wait_status = 0;
        pid2:pid_t = System.waitpid(self.pgrp, &status, System.WUNTRACED);

        if(System.WIFSTOPPED(status)) {
            rcode:int = System.WSTOPSIG(status) +128;

            terminfo2:termios = new termios();
            System.tcgetattr(0, terminfo2);

            self.terminfo = terminfo2;

            System.tcsetattr(0, System.TCSANOW, terminfo);
            System.tcsetpgrp(0, System.getpid());
        }
        elif(System.WIFSIGNALED(status)) {
            printf("Job<%s> is done.\n", array { self.title });

            Clover.jobs.delete(self);

            System.tcsetattr(0, System.TCSANOW, terminfo);
            System.tcsetpgrp(0, System.getpid());
        }
        else {
            Clover.jobs.delete(self);

            System.tcsetattr(0, System.TCSANOW, terminfo);
            System.tcsetpgrp(0, System.getpid());
        }
    }
}

class Clover 
{
    jobs: static EqualableList<Job>;

    def initialize(): static {
        inherit();

        Clover.jobs = new EqualableList<Job>();
    }
}

dynamic_class Command
{
    data:String;
    resultCode:int;

    controllingTerminalPrograms: static EqualableList<String>;

    def initialize() : static {
        Command.controllingTerminalPrograms = equalable_list { "vim", "less", "top", "lv", "htop", "emacs", "nano", "vi", "fd", "mc" };
    }

    def initialize() {
        self.data = "";
        self.resultCode = 0;
    }
    def initialize(value:Command) {
        self.setValue(value);
    }
    def initialize(data:String, result_code:int) {
        self.data = data;
        self.resultCode = result_code;
    }

    def setValue(value:Command) {
        self.data = value.data;
        self.resultCode = value.resultCode;
    }

    def callingMethod(method_name:String, params:Array<Object>) : static Command {
        params2:List<String> = params.toList().select(lambda(item:Object):bool { return item.className().equals("String");}).toAnonymous();

        if(Command.controllingTerminalPrograms.indexOf(method_name) != -1) {
            return Command.excuteCommandWithControllingTerminal(method_name, params2);
        }
        else {
            str:String = null;
            return Command.executeCommand(method_name, params2, str);
        }
    }

    def callingMethod(method_name:String, params:Array<Object>) : Command {
        params2:List<String> = params.toList().select(lambda(item:Object):bool { return item.className().equals("String");}).toAnonymous();

        if(Command.controllingTerminalPrograms.indexOf(method_name) != -1) {
            return Command.excuteCommandWithControllingTerminalByPipe(method_name, params2, self.data);
        }
        else {
            return Command.executeCommand(method_name, params2, self.data);
        }
    }

    def toString():String {
        return self.data;
    }

    def write(path:String, permission:mode_t): Command {
        f:File = new File(path, System.O_CREAT|System.O_TRUNC|System.O_WRONLY, permission);

        buf:Buffer = self.data.toBuffer();
        f.write(buf, buf.len);

        f.close();

        return self;
    }

    def write(path:String): Command {
        return self.write(path,0644);
    }

    def executeCommand(method_name:String, params:List<String>, pipe_data:String) : static Command
    {
        child2parent_write_fd:int = 0;
        child2parent_read_fd:int = 0;
        parent2child_write_fd:int = 0;
        parent2child_read_fd:int = 0;

        System.pipe(&child2parent_read_fd, &child2parent_write_fd);
        System.pipe(&parent2child_read_fd, &parent2child_write_fd);

        pid:pid_t = System.fork(closure() {
            System.close(parent2child_write_fd);
            System.close(child2parent_read_fd);

            System.dup2(parent2child_read_fd, 0);
            System.dup2(child2parent_write_fd, 1);

            System.close(parent2child_read_fd);
            System.close(child2parent_write_fd);

            try {
                System.execvp(method_name, params);
            } catch(e:Exception) {
                buffer:Buffer = e.message.append("\n").toBuffer();
                System.write(2, buffer, buffer.len);
                System.exit(2);
            }
        });

        System.close(parent2child_read_fd);
        System.close(child2parent_write_fd);

        if(!pipe_data.identifyWith(null)) {
            buffer:Buffer = pipe_data.toBuffer();
            System.write(parent2child_write_fd, buffer, buffer.len);
        }
        System.close(parent2child_write_fd);

        child_output:Buffer = B"";
        
        while(true) {
            pipe_data:Buffer = new Buffer(128.to_ulong);

            readed_byte:int = System.read(child2parent_read_fd, pipe_data, 128.to_ulong);

            if(readed_byte == 0) {
                break;
            }

            child_output.append(pipe_data);
        }

        System.close(child2parent_read_fd);

        status: wait_status = 0;

        pid2:pid_t = System.waitpid(pid, &status, System.WUNTRACED);

        return new Command(child_output.toString(), System.WEXITSTATUS(status));
    }

    def excuteCommandWithControllingTerminalByPipe(method_name:String, params:List<String>, pipe_data:String): static Command
    {
        parent2child_write_fd:int = 0;
        parent2child_read_fd:int = 0;

        System.pipe(&parent2child_read_fd, &parent2child_write_fd);

        pid:pid_t = System.fork(closure() {
            System.close(parent2child_write_fd);

            pid:pid_t = System.getpid();

            System.setpgid(0, 0);
            System.tcsetpgrp(0, pid);

            System.dup2(parent2child_read_fd, 0);
            System.close(parent2child_read_fd);

            try {
                System.execvp(method_name, params);
            } catch(e:Exception) {
                buffer:Buffer = e.message.append("\n").toBuffer();
                System.write(2, buffer, buffer.len);
                System.exit(2);
            }
        });

        System.setpgid(pid, pid);
        System.tcsetpgrp(0, pid);

        System.close(parent2child_read_fd);

        if(!pipe_data.identifyWith(null)) {
            buffer:Buffer = pipe_data.toBuffer();
            System.write(parent2child_write_fd, buffer, buffer.len);
        }
        System.close(parent2child_write_fd);

        status: wait_status = 0;

        pid2:pid_t = System.waitpid(pid, &status, System.WUNTRACED);

        if(System.WIFSTOPPED(status)) {
            rcode:int = System.WSTOPSIG(status) +128;
            title:String = method_name;

            terminfo:termios = new termios();
            System.tcgetattr(0, terminfo);

            job:Job = new Job(title, pid, terminfo);

            Clover.jobs.add(job);

            System.tcsetpgrp(0, System.getpid());

            return new Command("", rcode);
        }
        else {
            System.tcsetpgrp(0, System.getpid());

            return new Command("", System.WEXITSTATUS(status));
        }
    }
}

